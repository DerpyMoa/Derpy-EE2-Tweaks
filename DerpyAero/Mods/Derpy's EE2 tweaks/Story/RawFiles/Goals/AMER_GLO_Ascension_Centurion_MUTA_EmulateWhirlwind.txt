Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//While wielding a two-handed weapon, your Centurion reaction range becomes 10m.

PROC PROC_AMER_KeywordStat_Centurion_PreFire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_EmulateWhirlwind", _)
AND
QRY_AMER_GEN_PlayerGetWeaponStyle(_Char)
AND
DB_AMER_GEN_OUTPUT_String("TwoHanded")
AND
QRY_Derpy_CheckIfArenaRange(_Char, _Target)
THEN
PROC_AMER_TempStat_ExtendedStat_Add(_Char, "FakeMeleeHit", "", "", "Derpy_Centurion_FakeMeleeHit", 1.0, 2000, "Attack");


QRY QRY_Derpy_CheckIfArenaRange((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
NOT DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
THEN
SetVisible(_Char, 0);


QRY QRY_Derpy_CheckIfArenaRange((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
AND
GetDistanceTo(_Char, _Target, _Distance)
AND
_Distance > 4.5
THEN
SetVisible(_Char, 0);


//===========================================================================================================


QRY QRY_AMER_KeywordStat_Centurion_OneshotOverride((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_EmulateWhirlwind", _)
AND
QRY_AMER_GEN_PlayerGetWeaponStyle(_Char)
AND
DB_AMER_GEN_OUTPUT_String("TwoHanded")
AND
QRY_Derpy_CheckIfArena(_Char, _Target)
THEN
DB_Derpy_CenturionBlink_Participants(_Char, _Target);
PlayEffect(_Char, "RS3_FX_Skills_Warrior_BlinkStrike_Prepare_01");
ProcObjectTimer(_Char, "Derpy_CenturionBlink_Hit", 300);
ProcObjectTimer(_Char, "Derpy_CenturionBlink_Appear", 600);


PROC PROC_AMER_Reaction_GetSelfFX_Manip((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (STRING)_ReactionID, (STRING)_RequestID)
AND
DB_Derpy_CenturionBlink_Participants(_Char, _Target)
AND
DB_AMER_GEN_OUTPUT_String(_FX)
AND
DB_AMER_GEN_OUTPUT_String_B(_Bone)
THEN
NOT DB_AMER_GEN_OUTPUT_String(_FX);
NOT DB_AMER_GEN_OUTPUT_String_B(_Bone);


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "Derpy_CenturionBlink_Hit")
AND
DB_Derpy_CenturionBlink_Participants(_Char, _Target)
THEN
NOT DB_Derpy_CenturionBlink_Participants(_Char, _Target);
PlayEffect(_Char, "RS3_FX_Skills_Warrior_BlinkStrike_Reappear_01");
PROC_AMER_TempStat_ThrowTimerEvent(_Char, "Attack");    //Manually throw the temptstat timer, because we aren't performing the basic attack that would normally do it in this case.
PROC_AMER_GEN_BasicAttackObjectStarted(_Target, _Char, _Char, "Ascension_Centurion_MUTA_EmulateWhirlwind");
PROC_AMER_GEN_CharacterCauseRemoteProjectileDamage(_Char, _Target, "Projectile_AMER_SCRIPT_BasicHit");
PlayEffect(_Target, "RS3_FX_Skills_Warrior_BlinkStrike_Impact_01");


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "Derpy_CenturionBlink_Appear")
THEN
SetVisible(_Char, 1);
PlayEffect(_Char, "RS3_FX_Skills_Warrior_BlinkStrike_Reappear_01");


//===========================================================================================================


QRY QRY_Derpy_CheckIfArena((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
NOT DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
THEN
PlayAnimation(_Char, "skill_cast_ground_fire_01_cast", "");


QRY QRY_Derpy_CheckIfArena((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
AND
GetDistanceTo(_Char, _Target, _Distance)
AND
_Distance > 4.5
THEN
PlayAnimation(_Char, "skill_cast_ground_fire_01_cast", "");


//===========================================================================================================


PROC PROC_AMER_Reaction_Conditions_Additional_WithinWeaponRange_GetRange_Manip((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, "AMER_Centurion", (STRING)_RequestID)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_EmulateWhirlwind", _)
AND
QRY_AMER_GEN_PlayerGetWeaponStyle(_Char)
AND
DB_AMER_GEN_OUTPUT_String("TwoHanded")
AND
DB_AMER_GEN_OUTPUT_Real(_OldRange)
AND
_OldRange < 10.0
THEN
NOT DB_AMER_GEN_OUTPUT_Real(_OldRange);
DB_AMER_GEN_OUTPUT_Real(10.0);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"