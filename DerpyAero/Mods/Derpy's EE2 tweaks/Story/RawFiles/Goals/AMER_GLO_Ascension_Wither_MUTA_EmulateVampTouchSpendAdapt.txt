Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//If you have 4 or more stacks of Adaptation when you perform a basic attack on an enemy with Withered, lose 1 stack of Adaptation and emulate the effects of Vampiric Touch.

PROC PROC_AMER_GEN_BasicAttackObjectStarted(_Defender, (REAL)_X, (REAL)_Y, (REAL)_Z, _Owner, _Attacker, _)
AND
DB_AMER_Ascension_SpecialLogic(_Attacker, "Ascension_Wither_MUTA_EmulateVampTouchSpendAdapt", _)
THEN
PROC_AMER_TempStat_HitStatus_ThrowProc(_Attacker, "Ascension_Wither_MUTA_EmulateVampTouchSpendAdapt", 2000, "");


PROC PROC_AMER_HitStatus_Event((CHARACTERGUID)_Defender, (CHARACTERGUID)_Attacker, "Ascension_Wither_MUTA_EmulateVampTouchSpendAdapt")
AND
DB_AMER_KeywordStat_Adaptation_CurrentStack(_Attacker, _StackCount)
AND
_StackCount >= 4
AND
ObjectExists(_Defender, 1)
AND
ObjectIsCharacter((CHARACTERGUID)_Defender, 1)
AND
HasActiveStatus(_Defender, "AMER_WITHER", 1)
AND
CharacterIsEnemy(_Defender, _Attacker, 1)
THEN
PROC_AMER_TempStat_HitStatus_ThrowProc_Clear(_Attacker, "Ascension_Wither_MUTA_EmulateVampTouchSpendAdapt");
PROC_AMER_KeywordStat_Adaptation_AddStack(_Attacker, _Attacker, -1);
PlayEffect(_Defender, "RS3_FX_Skills_VampiricHunder_Impact_Root_01", "Dummy_BodyFX");
PROC_AMER_GEN_CharacterCauseRemoteProjectileDamage(_Attacker, _Defender, "Projectile_AMER_SCRIPT_VampiricTouch");
PROC_AMER_GEN_CharacterCombatLogText(_Attacker, "AMER_Ascension_Wither_MUTA_EmulateVampTouchSpendAdapt");


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"