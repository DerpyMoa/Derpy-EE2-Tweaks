Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION


PROC PROC_AMER_Spells_SkillPreCast((CHARACTERGUID)_Char, "Shout_Apotheosis", (INTEGER)_InfusCount, (STRING)_Type, (STRING)_Element)
AND
_InfusCount < 3
THEN
PROC_AMER_TempStat_StatusMod_AddExtendedStat(_Char, "APOTHEOSIS", "SourceGen_InfiniteDuration", "", "", "", 1.0, "Apotheosis_Base", 0, 2000, "SkillCast");


PROC PROC_AMER_Spells_SkillCast((CHARACTERGUID)_Char, "Shout_Apotheosis", 0, (STRING)_Type, (STRING)_Element)
THEN
ApplyStatus(_Char, "APOTHEOSIS", 12.0, 0, _Char);


//====================================================================================================================================


QRY QRY_AMER_Source_Infusion_SkillCast((CHARACTERGUID)_Char, "Shout_Apotheosis", 1)
AND
QRY_AMER_GEN_Scaling_GetScaledValue(_Char, 3.0, 0.1, 0.0, "Ability", "Polymorph")	//Base of 2 turns +1 (+0.1 per Poly).
AND
DB_AMER_GEN_OUTPUT_Real(_Scaling)
AND
Integer(_Scaling, _Turns)
AND
Real(_Turns, _TurnsReal)	//Cast to Int -> Real to round the float, this is done because a duration of, for example, 12.3, would in-game round to a 3-turn status. We want the result rounded down.
AND
RealProduct(_TurnsReal, 6.0, _Duration)
THEN
PROC_AMER_Source_Infusion_AddRefund(_Char, 1, 0);
ApplyStatus(_Char, "APOTHEOSIS", _Duration, 0, _Char);


//====================================================================================================================================


QRY QRY_AMER_Source_Infusion_SkillCast((CHARACTERGUID)_Char, "Shout_Apotheosis", 2)
AND
QRY_AMER_GEN_Scaling_GetScaledValue(_Char, 4.0, 0.3, 0.0, "Ability", "Polymorph")
AND
DB_AMER_GEN_OUTPUT_Real(_Scaling)
AND
Integer(_Scaling, _Turns)
AND
Real(_Turns, _TurnsReal)
AND
RealProduct(_TurnsReal, 6.0, _Duration)
THEN
PROC_AMER_Source_Infusion_AddRefund(_Char, 2, 0);
ApplyStatus(_Char, "APOTHEOSIS", _Duration, 0, _Char);


//====================================================================================================================================


QRY QRY_AMER_Source_Infusion_SkillPreCast((CHARACTERGUID)_Char, "Shout_Apotheosis", 3)
THEN
PROC_AMER_TempStat_StatusMod_AddExtendedStat(_Char, "Derpy_Apotheosis", "SourceGen_AddPointsGranted", "", "", "", 1.0, "Apotheosis_Infus3", 0, 2000, "SkillCast");
PROC_AMER_TempStat_StatusMod_AddExtendedStat(_Char, "Derpy_Apotheosis", "Apotheosis_CombatEnd", "", "", "", 1.0, "Apotheosis_Infus3", 0, 2000, "SkillCast");


QRY QRY_AMER_Source_Infusion_SkillCast((CHARACTERGUID)_Char, "Shout_Apotheosis", 3)
THEN
PROC_AMER_Source_Infusion_AddRefund(_Char, 0, 1);
ApplyStatus(_Char, "Derpy_Apotheosis", -1.0, 0, _Char);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Spells_Polymorph"