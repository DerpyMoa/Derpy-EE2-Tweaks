Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

PROC PROC_AMER_Combat_TurnStarted((CHARACTERGUID)_Char, 0)
AND
DB_AMER_ExtendedStat_AddedStat(_Char, "TeslaCoil_SI3", _, _, _, _)
AND
QRY_AMER_GEN_GetOwner(_Char)
AND
DB_AMER_GEN_OUTPUT_Char(_Owner)
THEN
PROC_AMER_GEN_Shoot_Delayed("Explosion", _Owner, _Char, "Projectile_Derpy_SCRIPT_TeslaCoil_SI1", 0, 200);
ApplyStatus(_Char, "AMER_CHARGED", 12.0, 1, _Owner);
ApplyStatus(_Char, "Derpy_TeslaCoil_Conductor_2", 6.0, 1, _Char);


PROC PROC_AMER_GEN_FilteredStatus_Removed((CHARACTERGUID)_Char, (CHARACTERGUID)_Source, "AMER_SUMMONLIFE")
AND
DB_AMER_ExtendedStat_AddedStat(_Char, "TeslaCoil_SI3", _, _, _, _)
AND
GetTemplate(_Char, "Summons_Ward_Conductor_e6e4943d-c922-4881-b5b0-670580c15bf8")
THEN
ProcObjectTimer(_Char, "TeslaCoil_DelayedDeath_SI3", 500);


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "TeslaCoil_DelayedDeath_SI3")
THEN
PROC_AMER_GEN_CharacterDie(_Char, 0, "Electrocution", NULL_00000000-0000-0000-0000-000000000000);


PROC PROC_AMER_GEN_FilteredStatus_Applied((CHARACTERGUID)_Char, (CHARACTERGUID)_Source, "AMER_CHARGED", (INTEGER)_Turns)
AND
DB_AMER_ExtendedStat_AddedStat(_Char, "TeslaCoil_SI3", _, _, _, _)
AND
NOT DB_AMER_Combat_ActiveChar(_, _, _, _Char)
AND
GetPosition(_Char, _X, _Y, _Z)
AND
RealSum(_Y, 1.25, _YAdjusted)
AND
QRY_AMER_GEN_GetNearestInCombat(_Char, _Char, _Char, 13.0, 42)	//Visible living enemies.
AND
DB_AMER_GEN_OUTPUT_NearestInCombat(_Nearest, _)
AND
QRY_AMER_GEN_GetOwner(_Char)
AND
DB_AMER_GEN_OUTPUT_Char(_Owner)
THEN
PROC_Derpy_TeslaCoil_SI3(_Char, _Owner, _Nearest, _X, _YAdjusted, _Z);


PROC PROC_Derpy_TeslaCoil_SI3((CHARACTERGUID)_Char, (CHARACTERGUID)_Owner, (CHARACTERGUID)_Nearest, (REAL)_X, (REAL)_YAdjusted, (REAL)_Z)
AND
HasActiveStatus(_Char, "Derpy_TeslaCoil_Conductor_2", 1)
THEN
RemoveStatus(_Char, "Derpy_TeslaCoil_Conductor_2");
ApplyStatus(_Char, "Derpy_TeslaCoil_Conductor_1", 6.0, 0, _Char);
PROC_AMER_GEN_ShootProjectileAt_Remote(_Owner, _X, _YAdjusted, _Z, _Nearest, "Projectile_LightningBolt", 2000);


PROC PROC_Derpy_TeslaCoil_SI3((CHARACTERGUID)_Char, (CHARACTERGUID)_Owner, (CHARACTERGUID)_Nearest, (REAL)_X, (REAL)_YAdjusted, (REAL)_Z)
AND
HasActiveStatus(_Char, "Derpy_TeslaCoil_Conductor_1", 1)
THEN
RemoveStatus(_Char, "Derpy_TeslaCoil_Conductor_1");
PROC_AMER_GEN_ShootProjectileAt_Remote(_Owner, _X, _YAdjusted, _Z, _Nearest, "Projectile_LightningBolt", 2000);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GEN_Stats_ExtendedStats"