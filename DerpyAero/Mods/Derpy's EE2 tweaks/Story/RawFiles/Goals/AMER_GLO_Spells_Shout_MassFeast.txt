Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

PROC PROC_AMER_Spells_SkillPreCast((CHARACTERGUID)_Char, "Shout_MassFeast", (INTEGER)_InfusCount, (STRING)_Type, (STRING)_Element)
THEN
DB_Derpy_MassFeast_Owner(_Char);


PROC PROC_AMER_Spells_SkillCast((CHARACTERGUID)_Char, "Shout_MassFeast", (INTEGER)_InfusCount, (STRING)_Type, (STRING)_Element)
THEN
PROC_AMER_GEN_IterateCharsAroundObject(_Char, 13.0, _Char, 4, "Derpy_MassFeast_SkittererIterator");	//	Allies.


PROC PROC_AMER_GEN_IterateCharsAroundObject_Iterated((INTEGER)_RequestID, "Derpy_MassFeast_SkittererIterator", (CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
AND
IsTagged(_Char, "Derpy_Skitterer", 1)
AND
GetPosition(_Char, _X, _Y, _Z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Voodoo_Blood_Swarm_Target_01", _X, _Y, _Z);
PlayEffectAtPosition("RS3_FX_Skills_Voodoo_BloodBubble_Impact_01", _X, _Y, _Z);
PlayBeamEffect(_Char, _SourceChar, "AMER_RS3_FX_Beams_Blood_Instant", "Dummy_BodyFX", "Dummy_BodyFX");


//==========================================================================================================


IF
NRD_OnHit((CHARACTERGUID)_Target, (CHARACTERGUID)_Char, _Damage, _Handle)
AND
NRD_StatusGetString(_Target, _Handle, "SkillId", "Projectile_Grenade_ArmorPiercing_BoneHand")
AND
DB_AMER_GEN_DamageTypeEnum(_TypeIndex, _DamageType)
AND
NRD_HitStatusGetDamage(_Target, _Handle, _DamageType, _Amount)
AND
_Amount != 0
AND
Real(_Amount, _AmountReal)
AND
QRY_AMER_GEN_GetOwner(_Char)
AND
DB_AMER_GEN_OUTPUT_Char(_Owner)
AND
DB_Derpy_MassFeast_Owner(_Owner)
THEN
PROC_AMER_GEN_Heal(_Owner, _AmountReal, "Vitality", _Owner, 1);


//==========================================================================================================



PROC PROC_AMER_GEN_SummonDied(_Char, _Owner)
AND
DB_Derpy_MassFeast_Owner(_Owner)
AND
GetTemplate(_Char, "AMER_Extinction_BoneHand_A_b01d0e46-aca1-4865-9718-1be6adff888b")
THEN
PROC_Derpy_MassFeast_Count();


PROC PROC_Derpy_MassFeast_Count()
AND
DB_Derpy_MassFeast_KillCount(_OldCount)
AND
IntegerSum(_OldCount, 1, _NewCount)
THEN
NOT DB_Derpy_MassFeast_KillCount(_OldCount);
DB_Derpy_MassFeast_KillCount(_NewCount);


PROC PROC_Derpy_MassFeast_Count()
AND
NOT DB_Derpy_MassFeast_KillCount(_)  //Notice this line here, the way you have it, it will create *multiple* count DBs
THEN
DB_Derpy_MassFeast_KillCount(1);


IF 
DB_Derpy_MassFeast_KillCount(3)
AND
DB_Derpy_MassFeast_Owner(_Char)
AND
GetPosition(_Char, _X, _Y, _Z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Totem_Target_Nebula_01", _X, _Y, _Z);
PROC_AMER_Summoning_MakeNextNPC(_Char, "AMER_Extinction_BoneHand_B_4b27963b-4f95-471a-acb4-ba11fcedd033", 1);
PROC_AMER_Summoning_SnapNextToValidPos(_Char, "AMER_Extinction_BoneHand_B_4b27963b-4f95-471a-acb4-ba11fcedd033", 1);
PROC_AMER_GEN_CharacterCauseExplosionAtPos(_Char, _X, _Y, _Z, "Projectile_Derpy_SCRIPT_Summon_Death_EmpoweredSkitterer");


//==========================================================================================================


PROC PROC_AMER_Spells_SkillCast_Finished((CHARACTERGUID)_Char, "Shout_MassFeast", (INTEGER)_InfusCount, (STRING)_Type, (STRING)_Element)
THEN
ProcObjectTimer(_Char, "Derpy_MassFeast_DelayedCleanup", 100);


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "Derpy_MassFeast_DelayedCleanup")
AND
DB_Derpy_MassFeast_Owner(_Char)
THEN
NOT DB_Derpy_MassFeast_Owner(_Char);


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "Derpy_MassFeast_DelayedCleanup")
AND
DB_Derpy_MassFeast_KillCount(_NewCount)
THEN
NOT DB_Derpy_MassFeast_KillCount(_NewCount);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Spells_Special"