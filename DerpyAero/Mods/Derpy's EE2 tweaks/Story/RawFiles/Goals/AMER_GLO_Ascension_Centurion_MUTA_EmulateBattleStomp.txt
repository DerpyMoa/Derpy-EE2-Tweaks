Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Thrice per round while wielding a melee weapon, instead of performing a basic attack, Centurion emulates Battle Stomp and raises the target acquisition radius to 10m.

QRY QRY_AMER_KeywordStat_Centurion_OneshotOverride((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_EmulateBattleStomp", _)
AND
NOT QRY_AMER_GEN_PlayerHasRangedWeapon(_Char)
AND
GetPosition(_Target, _X, _Y, _Z)
AND
QRY_AMER_GEN_NPerRound(_Char, "Ascension_Centurion_MUTA_EmulateBattleStomp", 3)
AND
QRY_Derpy_CheckIfArena(_Char, _Target)
THEN
PROC_AMER_GEN_NPerRound_Add(_Char, "Ascension_Centurion_MUTA_EmulateBattleStomp", 1);
DB_Derpy_Ascension_CenturionStomp(_Char, _Target, _X, _Y, _Z);
PlaySound(_Char, "Skill_Warrior_GroundSmash_Impact_01");
ProcObjectTimer(_Char, "Derpy_CenturionStomp_Delay", 800);


PROC PROC_AMER_Reaction_GetSelfFX_Manip((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (STRING)_ReactionID, (STRING)_RequestID)
AND
DB_Derpy_Ascension_CenturionStomp(_Char, _Target, _X, _Y, _Z)
AND
DB_AMER_GEN_OUTPUT_String(_FX)
AND
DB_AMER_GEN_OUTPUT_String_B(_Bone)
THEN
NOT DB_AMER_GEN_OUTPUT_String(_FX);
NOT DB_AMER_GEN_OUTPUT_String_B(_Bone);


PROC ProcObjectTimerFinished((CHARACTERGUID)_Char, "Derpy_CenturionStomp_Delay")
AND
DB_Derpy_Ascension_CenturionStomp(_Char, _Target, _X, _Y, _Z)
THEN
NOT DB_Derpy_Ascension_CenturionStomp(_Char, _Target, _X, _Y, _Z);
CharacterLookAt(_Char, _Target);
PlaySound(_Char, "Proj_Gren_Tremor_Impact");
PlayEffect(_Char, "RS3_FX_SubEffects_FireTrail_02", "Dummy_L_HandFX");
PlayEffect(_Char, "RS3_FX_SubEffects_FireTrail_02", "Dummy_R_HandFX");
PROC_AMER_GEN_ShootConeAtPos(_Char, _X, _Y, _Z, "Cone_GroundSmash", "RS3_FX_Skills_Warrior_GroundSmash_Cast_01");
PROC_AMER_TempStat_ThrowTimerEvent(_Char, "Attack");    //Manually throw the temptstat timer, because we aren't performing the basic attack that would normally do it in this case.
PROC_AMER_GEN_BasicAttackObjectStarted(_Target, _Char, _Char, "Ascension_Centurion_MUTA_EmulateBattleStomp");


QRY QRY_Derpy_CheckIfArena((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
NOT DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
THEN
PlayAnimation(_Char, "skill_attack_power_01_cast", "");


QRY QRY_Derpy_CheckIfArena((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_WhirlwindConversion", _)
AND
GetDistanceTo(_Char, _Target, _Distance)
AND
_Distance > 4.5
THEN
PlayAnimation(_Char, "skill_attack_power_01_cast", "");


PROC PROC_AMER_Reaction_Conditions_Additional_WithinWeaponRange_GetRange_Manip((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, "AMER_Centurion", (STRING)_RequestID)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_EmulateBattleStomp", _)
AND
NOT QRY_AMER_GEN_PlayerHasRangedWeapon(_Char)
AND
QRY_AMER_GEN_NPerRound(_Char, "Ascension_Centurion_MUTA_EmulateBattleStomp", 3)
AND
DB_AMER_GEN_OUTPUT_Real(_OldRange)
AND
_OldRange < 10.0
THEN
NOT DB_AMER_GEN_OUTPUT_Real(_OldRange);
DB_AMER_GEN_OUTPUT_Real(10.0);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"