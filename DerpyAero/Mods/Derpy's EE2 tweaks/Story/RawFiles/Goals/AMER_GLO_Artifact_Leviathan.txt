Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//A Violent Strike is occuring.
PROC PROC_AMER_KeywordStat_ViolentStrikes_Fire((CHARACTERGUID)_Source, (CHARACTERGUID)_Target, (STRING)_Projectile)
AND
DB_AMER_Artifacts_EquippedEffects(_Source, "Artifact_Leviathan", _)
AND
DB_AMER_Source_Infusion_Current(_Source, _InfusCount)
AND
_InfusCount > 0
THEN
DB_AMER_Source_Infusion_RefundPoints(_Source, 0, 1);
PlayEffect(_Source, "AMER_RS3_FX_Artifact_Leviathan_TeleportCenter", "");
PROC_AMER_Source_Infusion_ExpendInfusion(_Source, "Artifact_Leviathan", 1, 1, 1);
CharacterStatusText(_Source, "Artifact_Leviathan");
ApplyStatus(_Source, "WET", 18.0, 0, _Source);
PROC_AMER_GEN_CharacterCombatLogText(_Source, "AMER_Artifact_Leviathan_Activated");
PROC_AMER_GEN_IterateCharsAroundObject(_Source, 8.0, _Source, 10, "Artifact_Leviathan_TPIterator");	//Living enemies that within 6.0m.

PROC PROC_AMER_GEN_IterateCharsAroundObject_Iterated((INTEGER)_RequestID, "Artifact_Leviathan_TPIterator", (CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
THEN
PROC_AMER_Artifacts_Leviathan_Teleport(_SourceChar, _Char);

PROC PROC_AMER_Artifacts_Leviathan_Teleport((CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
THEN
ApplyStatus(_Char, "WET", 18.0, 0, _SourceChar);

PROC PROC_AMER_Artifacts_Leviathan_Teleport((CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
AND
CombatGetIDForCharacter(_SourceChar, _Combat)
AND
CombatGetIDForCharacter(_Char, _Combat)	//Must be in the same combat as _SourceChar.
AND
IsBoss(_Char, 0)
AND
GetPosition(_SourceChar, _X, _Y, _Z)
AND
FindValidPosition(_X, _Y, _Z, 3.0, _Char, _XValid, _YValid, _ZValid)
THEN
PROC_AMER_GEN_TeleportWithFX_ToPos(_Char, _XValid, _YValid, _ZValid, 0, 1, "RS3_FX_GP_Impacts_Launch_Water_Slug_01", "RS3_FX_GP_Impacts_Grenade_WaterBalloon_01");

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Artifacts"