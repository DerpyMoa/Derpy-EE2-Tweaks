Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//Your damage is multiplicatively increased by 1% per Memory over 10, but you are unable to score Critical Hits.

IF
NRD_OnPrepareHit((CHARACTERGUID)_Defender, (CHARACTERGUID)_Attacker, _Damage, _HitHandle)
AND
CharacterHasTalent(_Attacker, "Memory", 1)
AND
CharacterGetAttribute(_Attacker, "Memory", _Mem)
AND
IntegerSubtract(_Mem, 10, _InvestedMem)
AND
Real(_InvestedMem, _InvestedMemReal)
AND
RealProduct(_InvestedMemReal, 0.01, _BonusPerc)
AND
DB_AMER_GEN_DamageTypeEnum(_TypeIndex, _DamageType)
AND
NRD_HitGetDamage(_HitHandle, _DamageType, _Amount)
AND
_Amount != 0
AND
Real(_Amount, _AmountReal)
AND
RealProduct(_AmountReal, _BonusPerc, _BonusAmount)
AND
Integer(_BonusAmount, _BonusAmountInt)
AND
IntegerSum(_Amount, _BonusAmountInt, _Result)
THEN
NRD_HitClearDamage(_HitHandle, _DamageType);
NRD_HitAddDamage(_HitHandle, _DamageType, _Result);
NRD_HitSetInt(_HitHandle, "CriticalRoll", 2);


IF
NRD_OnHit((CHARACTERGUID)_Defender, (CHARACTERGUID)_Attacker, _Damage, _Handle)
AND
CharacterHasTalent(_Attacker, "Memory", 1)
THEN
NRD_StatusSetInt(_Defender, _Handle, "CriticalHit", 0);
NRD_StatusSetInt(_Defender, _Handle, "Backstab", 0);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Talents"