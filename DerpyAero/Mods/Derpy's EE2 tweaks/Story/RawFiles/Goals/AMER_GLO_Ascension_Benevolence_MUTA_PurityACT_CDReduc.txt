Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//When you remove 3 stacks of Battered and/or Harried with Benevolence, activate Purity. Once per round, if it is on cooldown, reduce its remaining cooldown by 1 turn.

PROC PROC_AMER_KeywordStat_Benevolence_Finished(_Char, _TotalBattered, _TotalHarried, _TotalTargets)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Benevolence_MUTA_PurityACT_CDReduc", _)
AND
IntegerSum(_TotalBattered, _TotalHarried, _TotalStacks)
AND
_TotalStacks >= 3
AND
HasActiveStatus(_Char, "AMER_PURITY_COOLDOWN", _HasPurity)
THEN
PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc_HasPurity(_Char, _HasPurity);


PROC PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc_HasPurity((CHARACTERGUID)_Char, 0)
THEN
PROC_AMER_KeywordStat_Purity_Activate(_Char, _Char, _Char);


PROC PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc_HasPurity((CHARACTERGUID)_Char, 1)
AND
QRY_AMER_GEN_OncePerRound(_Char, "Ascension_Benevolence_MUTA_PurityACT_CDReduc")
AND
GetStatusTurns(_Char, "AMER_PURITY_COOLDOWN", _Turns)
AND
IntegerSubtract(_Turns, 1, _NewTurns)
THEN
DB_AMER_GEN_OncePerRound(_Char, "Ascension_Benevolence_MUTA_PurityACT_CDReduc");
PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc(_Char, _NewTurns);


PROC PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc((CHARACTERGUID)_Char, (INTEGER)_NewTurns)
THEN
PROC_AMER_GEN_CharacterCombatLogText(_Char, "reduced cooldown of Purity with The Pegasus.");
RemoveStatus(_Char, "AMER_PURITY_COOLDOWN");


PROC PROC_AMER_Ascension_Benevolence_MUTA_PurityACT_CDReduc((CHARACTERGUID)_Char, (INTEGER)_NewTurns)
AND
_NewTurns > 0
AND
Real(_NewTurns, _TurnsReal)
AND
RealProduct(_TurnsReal, 6.0, _Duration)
THEN
ApplyStatus(_Char, "AMER_PURITY_COOLDOWN", _Duration, 0, _Char);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"