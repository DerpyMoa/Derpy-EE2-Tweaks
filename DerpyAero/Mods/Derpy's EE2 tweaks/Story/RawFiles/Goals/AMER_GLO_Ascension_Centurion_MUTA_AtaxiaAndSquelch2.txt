Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//Apply up to Ataxia 2 and Squelched 2 for 2 turns to targets of Centurion reactions and gain +10% Critical Chance per enemy within 5m.

PROC PROC_AMER_KeywordStat_Centurion_PreFire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target)
AND
DB_AMER_Ascension_SpecialLogic(_Char, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2", _)
THEN
PROC_AMER_GEN_CountCharsAroundObject(_Char, 5.0, _Char, 10, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2");	//Count only living enemies within 5m.
PROC_AMER_TempStat_HitStatus_ThrowProc(_Char, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2", 2000, "Attack");


PROC PROC_AMER_GEN_CountCharsAroundObject_Returned((INTEGER)_RequestIndex, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2", (CHARACTERGUID)_Char, (INTEGER)_Count)
AND
Real(_Count, _CountReal)
AND
RealProduct(_CountReal, 10.0, _CritBonus)
THEN
PROC_AMER_TempStat_FlexStat_Add(_Char, "Stat", "CRITICALCHANCE", _CritBonus, 2000, "Attack");


PROC PROC_AMER_HitStatus_Event((CHARACTERGUID)_Defender, (CHARACTERGUID)_Attacker, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2")
AND
NOT DB_Derpy_Ascension_Centurion_MUTA_AtaxiaAndSquelch2_OncePerTarget(_Defender, _Attacker)
AND
CharacterIsEnemy(_Defender, _Attacker, 1)
THEN
DB_Derpy_Ascension_Centurion_MUTA_AtaxiaAndSquelch2_OncePerTarget(_Defender, _Attacker);
ApplyStatus(_Defender, "AMER_ATAXIA_2", 12.0, 0, _Attacker);
ApplyStatus(_Defender, "AMER_SQUELCHED_2", 12.0, 0, _Attacker);


PROC PROC_AMER_TempStat_HitStatus_Cleanup((CHARACTERGUID)_Attacker, "Ascension_Centurion_MUTA_AtaxiaAndSquelch2")
AND
DB_Derpy_Ascension_Centurion_MUTA_AtaxiaAndSquelch2_OncePerTarget(_Defender, _Attacker)
THEN
NOT DB_Derpy_Ascension_Centurion_MUTA_AtaxiaAndSquelch2_OncePerTarget(_Defender, _Attacker);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_UI_Ascension_NodeRewards"