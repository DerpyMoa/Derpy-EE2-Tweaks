Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

//Recover 2AP on basic attacks and weapon skills. Additionally, your damage is multiplicatively increased by 1% per Strength over 10.

IF
NRD_OnPrepareHit((CHARACTERGUID)_Defender, (CHARACTERGUID)_Attacker, _Damage, _HitHandle)
AND
CharacterHasTalent(_Attacker, "Ambidextrous", 1)
AND
CharacterHasSkill(_Attacker, "Target_SingleHandedAttack", 1)
AND
CharacterGetAttribute(_Attacker, "Strength", _Str)
AND
IntegerSubtract(_Str, 10, _InvestedStr)
AND
Real(_InvestedStr, _InvestedStrReal)
AND
RealProduct(_InvestedStrReal, 0.01, _BonusPerc)
AND
DB_AMER_GEN_DamageTypeEnum(_TypeIndex, _DamageType)
AND
NRD_HitGetDamage(_HitHandle, _DamageType, _Amount)
AND
_Amount != 0
AND
Real(_Amount, _AmountReal)
AND
RealProduct(_AmountReal, _BonusPerc, _BonusAmount)
AND
Integer(_BonusAmount, _BonusAmountInt)
AND
IntegerSum(_Amount, _BonusAmountInt, _Result)
THEN
NRD_HitClearDamage(_HitHandle, _DamageType);
NRD_HitAddDamage(_HitHandle, _DamageType, _Result);


//========================================================================================================================


PROC PROC_AMER_GEN_BasicAttackObjectStarted((CHARACTERGUID)_Defender, (REAL)_X, (REAL)_Y, (REAL)_Z, (CHARACTERGUID)_Owner, (CHARACTERGUID)_Attacker, (STRING)_AttackID)
AND
CharacterHasTalent(_Attacker, "Ambidextrous", 1)
AND
CharacterHasSkill(_Attacker, "Target_SingleHandedAttack", 1)
THEN
PROC_AMER_ExtendedStat_CharacterAddStat(_Attacker, "BasicAttackRecoverAP_Oneshot", "", "", "", 2.0);


PROC PROC_AMER_Spells_SkillPreCast(_Char, _Skill, _, _, _)
AND
CharacterHasTalent(_Char, "Ambidextrous", 1)
AND
CharacterHasSkill(_Char, "Target_SingleHandedAttack", 1)
AND
NRD_StatGetString(_Skill, "UseWeaponDamage", "Yes")
THEN
PROC_AMER_ExtendedStat_CharacterAddStat(_Char, "BasicAttackRecoverAP_Oneshot", "", "", "", 2.0);


EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Talents"