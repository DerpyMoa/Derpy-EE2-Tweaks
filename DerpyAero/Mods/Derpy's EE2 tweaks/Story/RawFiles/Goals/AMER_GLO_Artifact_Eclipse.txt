Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION

IF
NRD_OnStatusAttempt((CHARACTERGUID)_Char, _Status, _Handle, _Source)
THEN
PROC_Derpy_EclipseStatusChecks(_Char, _Status);


PROC PROC_Derpy_EclipseStatusChecks((CHARACTERGUID)_Char, "AMER_ENTHRALLED_3")
AND
HasActiveStatus(_Char, "AMER_ENTHRALLED_3", 1)
THEN
DB_Derpy_SubjugatedCheck(_Char);


PROC PROC_Derpy_EclipseStatusChecks((CHARACTERGUID)_Char, "AMER_TERRIFIED_3")
AND
HasActiveStatus(_Char, "AMER_TERRIFIED_3", 1)
THEN
DB_Derpy_TerrifiedCheck(_Char);


PROC PROC_AMER_GEN_FilteredStatus_Removed((CHARACTERGUID)_Char, (CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, "AMER_ENTHRALLED_3")
AND
NOT QRY_Derpy_SubjugatedDBCheck(_Char)
AND
DB_AMER_Artifacts_EquippedEffects(_ArtifactWielder, "Artifact_Eclipse", _)
AND
CharacterIsEnemy(_Char, _ArtifactWielder, 1)
THEN
PROC_AMER_Reaction_Request(_ArtifactWielder, _Char, "Artifact_Eclipse", "", "skill_cast_target_voodoo_01_cast", 1);

PROC PROC_AMER_GEN_FilteredStatus_Removed((CHARACTERGUID)_Char, (CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, "AMER_TERRIFIED_3")
AND
NOT QRY_Derpy_TerrifiedDBCheck(_Char)
AND
DB_AMER_Artifacts_EquippedEffects(_ArtifactWielder, "Artifact_Eclipse", _)
AND
CharacterIsEnemy(_Char, _ArtifactWielder, 1)
THEN
PROC_AMER_Reaction_Request(_ArtifactWielder, _Char, "Artifact_Eclipse", "", "skill_cast_target_voodoo_01_cast", 1);


QRY QRY_Derpy_SubjugatedDBCheck((CHARACTERGUID)_Char)
AND
DB_Derpy_SubjugatedCheck(_Char)
THEN
NOT DB_Derpy_SubjugatedCheck(_Char);


QRY QRY_Derpy_TerrifiedDBCheck((CHARACTERGUID)_Char)
AND
DB_Derpy_TerrifiedCheck(_Char)
THEN
NOT DB_Derpy_TerrifiedCheck(_Char);


PROC PROC_AMER_Reaction_Fire((CHARACTERGUID)_Char, (CHARACTERGUID)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z, "Artifact_Eclipse", (STRING)_RequestID, (INTEGER)_RitualBool)
AND
GetPosition(_Target, _X, _Y, _Z)
THEN
PlayEffect(_Char, "RS3_FX_Skills_Voodoo_Cast_Aoe_Voodoo_Purple_Root_01_Old", "Dummy_Root");
PlayEffectAtPosition("RS3_FX_GP_Impacts_Grenade_Terror_01", _X, _Y, _Z);
PROC_AMER_GEN_IterateCharsAroundObject(_Target, 5.0, _Char, 10, "Artifact_Eclipse_Iterator");	//Living enemies within 5.0m of _Target.

PROC PROC_AMER_GEN_IterateCharsAroundObject_Iterated((INTEGER)_RequestID, "Artifact_Eclipse_Iterator", (CHARACTERGUID)_SourceChar, (CHARACTERGUID)_Char)
THEN
ApplyStatus(_Char, "AMER_MUTED", 12.0, 1, _SourceChar);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "AMER_GLO_Artifacts"